
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//Вставить содержимое обработчика
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ЗначениеЗаполнено(ШиринаОкна) Тогда
		ШиринаОкна = "День";
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(НачалоПериода1) Тогда
		НачалоПериода1 = НачалоДня(ТекущаяДата());
	КонецЕсли;
	СформироватьГрафикМониторинга();
КонецПроцедуры

&НаСервере
Процедура СформироватьГрафикМониторинга()
	
	Перем Выборка, КоличествоОшибок, КоличествоПровалов, КоличествоПропущенных, КоличествоТестовыхСлучаев, КоличествоУспешных, СерияОшибки, СерияПадения, СерияПропуски, СерияУспешно, Статусы, ТочкаДиаграммы;
	
	// нет свойств и не надо рисовать
	Если Свойства.Количество()=0 Тогда
		Возврат; 
	КонецЕсли;
	
	Диаграмма_Мониторинга.Очистить();

	
	// настройки графика
	Если НЕ ЗначениеЗаполнено(ВидГрафика_Мониторинга) Тогда
		ВидГрафика_Мониторинга="Гистограмма";
	КонецЕсли;
	Элементы.ВидГрафика_Мониторинга.РежимВыбораИзСписка = Истина;
	Элементы.ВидГрафика_Мониторинга.РедактированиеТекста = Ложь;
	Элементы.ВидГрафика_Мониторинга.СписокВыбора.Очистить();
	Элементы.ВидГрафика_Мониторинга.СписокВыбора.Добавить("Гистограмма");
	Элементы.ВидГрафика_Мониторинга.СписокВыбора.Добавить("График");
	Элементы.ВидГрафика_Мониторинга.СписокВыбора.Добавить("ГистограммаОбъемная");
	Элементы.ВидГрафика_Мониторинга.СписокВыбора.Добавить("ГрафикСОбластями");
	

	Диаграмма_Мониторинга.ТипДиаграммы=ТипДиаграммы[ВидГрафика_Мониторинга];
	
	СерияОкон = новый Соответствие();
	
	
	
	Для каждого стр из Свойства Цикл 
		СерияОкно1 = Диаграмма_Мониторинга.УстановитьСерию(стр.Значение);
		СерияОкон.Вставить(стр.Значение,СерияОкно1);
	КонецЦикла;
	
	
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СРЕДНЕЕ(ВЫБОР
	|		КОГДА СобытияЗамера.Свойство.Процент
	|			ТОГДА СобытияЗамера.ЗначениеЧисло * 100
	|		ИНАЧЕ СобытияЗамера.ЗначениеЧисло
	|	КОНЕЦ) КАК Значение,
	|	СобытияЗамера.Свойство КАК Свойство,
	|	-РАЗНОСТЬДАТ(СобытияЗамера.Ссылка.ДатаСобытия, &Интервал1Начало, &ЧАС) КАК КоличествоЧасовОтНачалаИнтервала
	|ПОМЕСТИТЬ ВтПовторенияИнтервал1
	|ИЗ
	|	Справочник.СобытияЗамера.КлючевыеСвойства КАК СобытияЗамера
	|ГДЕ
	|	СобытияЗамера.Ссылка.Владелец = &Замер
	|	И СобытияЗамера.Свойство В (&МассивСвойств)
	|	И СобытияЗамера.Ссылка.ДатаСобытия МЕЖДУ &Интервал1Начало И &Интервал1Окончание
	|СГРУППИРОВАТЬ ПО
	|	СобытияЗамера.Свойство,
	|	-РАЗНОСТЬДАТ(СобытияЗамера.Ссылка.ДатаСобытия, &Интервал1Начало, &ЧАС)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КоличествоЧасовОтНачалаИнтервала
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТЗ.Количество КАК КоличествоЧасовОтНачалаИнтервала
	|ПОМЕСТИТЬ ВтШкалаВремени
	|ИЗ
	|	&ТЗШкала как ТЗ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КоличествоЧасовОтНачалаИнтервала
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВтОбъединениеНаШкале.КоличествоЧасовОтНачалаИнтервала, ВтШкалаВремени.КоличествоЧасовОтНачалаИнтервала) КАК
	|		КоличествоЧасовОтНачалаИнтервала,
	|	ЕСТЬNULL(ВтОбъединениеНаШкале.Свойство, НЕОПРЕДЕЛЕНО) КАК Свойство,
	|	ЕСТЬNULL(ВтОбъединениеНаШкале.Значение, 0) КАК Значение
	|ИЗ
	|	ВтПовторенияИнтервал1 КАК ВтОбъединениеНаШкале
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВтШкалаВремени КАК ВтШкалаВремени
	|		ПО ВтОбъединениеНаШкале.КоличествоЧасовОтНачалаИнтервала = ВтШкалаВремени.КоличествоЧасовОтНачалаИнтервала
	|УПОРЯДОЧИТЬ ПО
	|	КоличествоЧасовОтНачалаИнтервала";
	
	ШиринаОкнаСекунды = ПолучитьШиринуОкна(ШиринаОкна);
	
	ОтносительнаяШкала = "ЧАС";
	
	Если ШиринаОкна="Час" Тогда
		ОтносительнаяШкала = "МИНУТА";
	ИначеЕсли ШиринаОкна="День" Тогда
		ОтносительнаяШкала = "ЧАС";	
	ИначеЕсли ШиринаОкна="Неделя" Тогда
		ОтносительнаяШкала = "ДЕНЬ";
	ИначеЕсли ШиринаОкна="Месяц" Тогда
		ОтносительнаяШкала = "ДЕНЬ";
	КонецЕсли;
	
	ТЗШкала = ПолучитьТЗШкала(ШиринаОкна);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"&ЧАС",ОтносительнаяШкала);
	
	Запрос.УстановитьПараметр("Замер",Замер);
	Запрос.УстановитьПараметр("Интервал1Начало",НачалоПериода1);
	Запрос.УстановитьПараметр("Интервал1Окончание",НачалоПериода1+ШиринаОкнаСекунды);
	Запрос.УстановитьПараметр("МассивСвойств",Свойства.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("ТЗШкала",ТЗШкала);
	
	
	Выборка = Запрос.Выполнить().Выбрать();
	

	Пока Выборка.Следующий() Цикл
		
		// установим точку
		Количество = ПолучитьПредставлениеКоличества(ШиринаОкна,Выборка.КоличествоЧасовОтНачалаИнтервала,НачалоПериода1);
		ТочкаДиаграммы = Диаграмма_Мониторинга.УстановитьТочку(Количество);
		
		//СерияОкно1.Цвет = новый Цвет(155,50,50);	
		
		СерияОкно = СерияОкон.Получить(Выборка.Свойство);
		Если НЕ СерияОкно=Неопределено Тогда
			// добавим значение	
			Диаграмма_Мониторинга.УстановитьЗначение(ТочкаДиаграммы,СерияОкно, Выборка.Значение);
		КонецЕсли;
		
		
	КонецЦикла;
	
	
	Диаграмма_Мониторинга.ВидПодписей=ВидПодписейКДиаграмме.Значение;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПредставлениеКоличества(ШиринаОкна,Количество,НачалоПериода)
	
	ПредставлениеКоличества = Количество;
	
	Если ШиринаОкна="Час" Тогда
		
	ИначеЕсли ШиринаОкна="День" Тогда
		
	ИначеЕсли ШиринаОкна="Неделя" Тогда
		
		Если Количество=0 Тогда
			ПредставлениеКоличества = "пн";
		ИначеЕсли Количество=1 Тогда
			ПредставлениеКоличества = "вт";
		ИначеЕсли Количество=2 Тогда
			ПредставлениеКоличества = "ср";
		ИначеЕсли Количество=3 Тогда
			ПредставлениеКоличества = "чт";
		ИначеЕсли Количество=4 Тогда
			ПредставлениеКоличества = "пт";
		ИначеЕсли Количество=5 Тогда
			ПредставлениеКоличества = "сб";
		ИначеЕсли Количество=6 Тогда
			ПредставлениеКоличества = "вс";
		ИначеЕсли Количество=7 Тогда
			ПредставлениеКоличества = "пн";
		КонецЕсли;
		
	ИначеЕсли ШиринаОкна="Месяц" Тогда
		
	КонецЕсли;
	
	
	Возврат ПредставлениеКоличества;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьТЗШкала(ШиринаОкна="День")
	
	МассивТипов = новый Массив;
	МассивТипов.Добавить(Тип("Число"));
	ОписаниеТипов = новый ОписаниеТипов(МассивТипов, , , , );
	
	ТЗ = новый ТаблицаЗначений();
	колонка = ТЗ.Колонки.Добавить("Количество",ОписаниеТипов);

	Если ШиринаОкна="Час" Тогда
		Для ш=0 по 60 Цикл
			стр = ТЗ.Добавить();
			стр.Количество = ш;
		КонецЦикла;		
	ИначеЕсли ШиринаОкна="День" Тогда
		Для ш=0 по 24 Цикл
			стр = ТЗ.Добавить();
			стр.Количество = ш;
		КонецЦикла;
	ИначеЕсли ШиринаОкна="Неделя" Тогда
		Для ш=0 по 7 Цикл
			стр = ТЗ.Добавить();
			стр.Количество = ш;
		КонецЦикла;		
	ИначеЕсли ШиринаОкна="Месяц" Тогда
		Для ш=0 по 30 Цикл
			стр = ТЗ.Добавить();
			стр.Количество = ш;
		КонецЦикла;		
	КонецЕсли;
	
	Возврат ТЗ;
	
КонецФункции

&НаКлиенте
Процедура Обновить(Команда)
	СформироватьГрафикМониторинга();
КонецПроцедуры

&НаКлиенте
Процедура Вперед(Команда)
	ШиринаОкнаСекунды = ПолучитьШиринуОкна(ШиринаОкна);
	НачалоПериода1 = НачалоПериода1 + ШиринаОкнаСекунды;
	СформироватьГрафикМониторинга();
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	ШиринаОкнаСекунды = ПолучитьШиринуОкна(ШиринаОкна);
	НачалоПериода1 = НачалоПериода1 - ШиринаОкнаСекунды;
	СформироватьГрафикМониторинга();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьШиринуОкна(ШиринаОкна="День")
	ШиринаОкнаСекунды = 24*60*60; // по умолчанию сутки
	
	Если ШиринаОкна="Час" Тогда
		ШиринаОкнаСекунды = 60*60;
	ИначеЕсли ШиринаОкна="Неделя" Тогда
		ШиринаОкнаСекунды = 7*24*60*60;
	ИначеЕсли ШиринаОкна="Месяц" Тогда
		ШиринаОкнаСекунды = 30*24*60*60;
	КонецЕсли;
	
	Возврат ШиринаОкнаСекунды;
КонецФункции

&НаКлиенте
Процедура ВидГрафика_МониторингаПриИзменении(Элемент)
	СформироватьГрафикМониторинга();
КонецПроцедуры

&НаКлиенте
Процедура ЗамерПриИзменении(Элемент)
	СформироватьГрафикМониторинга();
КонецПроцедуры

&НаКлиенте
Процедура ШиринаОкнаПриИзменении(Элемент)
	СформироватьГрафикМониторинга();
КонецПроцедуры


