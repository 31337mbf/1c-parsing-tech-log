&НаКлиенте 
Перем КэшРеквизитыСвойства;

&НаКлиенте
Процедура КлючевыеСвойстваВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ТекСвойство = Элементы.КлючевыеСвойства.ТекущиеДанные.Свойство;
	ТекСтрока = Элементы.КлючевыеСвойства.ТекущиеДанные.Значение;	
	ТекЧисло = Элементы.КлючевыеСвойства.ТекущиеДанные.ЗначениеЧисло;	
	Если ИспользуетсяМногострочныйРежим(ТекСвойство) Тогда 
		Т = Новый ТекстовыйДокумент();
		Т.ДобавитьСтроку(ТекСтрока);
		Т.Показать(Строка(ТекСвойство));
	ИначеЕсли ИспользуетсяЧисловойРежим(ТекСвойство) Тогда
		ПоказатьВводСтроки(Новый ОписаниеОповещения("ВводСтрокиЗавершение", ЭтотОбъект), ТекЧисло, Строка(ТекСвойство))
	Иначе
		ПоказатьВводСтроки(Новый ОписаниеОповещения("ВводСтрокиЗавершение", ЭтотОбъект), ТекСтрока, Строка(ТекСвойство))
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ИспользуетсяМногострочныйРежим(Свойство)
	МногострочныйРежим = Ложь;
	СвойстваСтрукт = КэшРеквизитыСвойства.Получить(Свойство);
	Если СвойстваСтрукт=Неопределено Тогда
		МногострочныйРежим = РеквизитыСвойстваСервер(Свойство).МногострочныйРежим;
		КэшРеквизитыСвойства.Вставить(Свойство, 
		Новый Структура ("МногострочныйРежим,ЧисловойРежим",МногострочныйРежим,Неопределено));
	ИначеЕсли СвойстваСтрукт.МногострочныйРежим=Неопределено Тогда
		МногострочныйРежим = РеквизитыСвойстваСервер(Свойство).МногострочныйРежим;
		СвойстваСтрукт.МногострочныйРежим = МногострочныйРежим;
		КэшРеквизитыСвойства.Вставить(Свойство, СвойстваСтрукт);
	Иначе
		МногострочныйРежим = СвойстваСтрукт.МногострочныйРежим;
	КонецЕсли;
	Возврат МногострочныйРежим;
КонецФункции

&НаКлиенте
Функция ИспользуетсяЧисловойРежим(Свойство)
	ЧисловойРежим = Ложь;
	СвойстваСтрукт = КэшРеквизитыСвойства.Получить(Свойство);
	Если СвойстваСтрукт=Неопределено Тогда
		ЧисловойРежим = РеквизитыСвойстваСервер(Свойство).ЧисловойРежим;
		КэшРеквизитыСвойства.Вставить(Свойство, 
			Новый Структура("МногострочныйРежим,ЧисловойРежим",Неопределено,ЧисловойРежим));
	ИначеЕсли СвойстваСтрукт.ЧисловойРежим=Неопределено Тогда
		ЧисловойРежим = РеквизитыСвойстваСервер(Свойство).ЧисловойРежим;
		СвойстваСтрукт.ЧисловойРежим = ЧисловойРежим;
		КэшРеквизитыСвойства.Вставить(Свойство, СвойстваСтрукт);
	Иначе
		ЧисловойРежим = СвойстваСтрукт.ЧисловойРежим;
	КонецЕсли;
	Возврат ЧисловойРежим;
КонецФункции

&НаСервереБезКонтекста
Функция РеквизитыСвойстваСервер(Свойство)
	Возврат СправочникиСерверПовтИсп.РеквизитыСвойства(Свойство);
КонецФункции

&НаКлиенте
Процедура ВводСтрокиЗавершение(Результат, Параметры) Экспорт
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеСвойства(Команда)
	Т = Новый ТекстовыйДокумент();
	Т.ДобавитьСтроку(Объект.ВсеСвойства);
	Т.Показать("Все свойства");
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	КэшРеквизитыСвойства = Новый Соответствие;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОписаниеСитуации(Команда)
	Если Элементы.КлючевыеСвойства.ТекущаяСтрока<>Неопределено Тогда
		ТД = Объект.КлючевыеСвойства.НайтиПоИдентификатору(Элементы.КлючевыеСвойства.ТекущаяСтрока);
		Если ЗначениеЗаполнено(ТД.ХешЗначения) Тогда
			ЗначенияЗаполнения = Новый Структура("Свойство,Значение,ХешЗначения");
			ЗаполнитьЗначенияСвойств(ЗначенияЗаполнения, ТД);
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ОткрытьФорму("Справочник.ИзвестныеСитуации.ФормаОбъекта", ПараметрыФормы, ЭтаФорма, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Иначе
			Состояние(СтрШаблон("Для свойства %1 отключено создание хеша", ТД.Свойство));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры



