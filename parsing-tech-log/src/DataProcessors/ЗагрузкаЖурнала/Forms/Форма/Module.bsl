&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	НовыйЗамер = 1;
	УстановитьСтраницы();
КонецПроцедуры

&НаСервере
Процедура УстановитьСтраницы()
	Если НовыйЗамер=1 Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСуществующий;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаНовый;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура НовыйЗамерПриИзменении1(Элемент)
	УстановитьСтраницы();
КонецПроцедуры


&НаСервере
Процедура ЗагрузитьЖурналНаСервере()
	
	ОбновлениеДанных.ЗагрузкаЖурналаПредварительноОчистить(Замер,КаталогТЖ);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВФоновомРежиме(Команда)
	Если ПроверкаВозможностиЗагрузки() = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	ЗапуститьЗагрузкуНаСервере();
	
	Если ЗначениеЗаполнено(КлючФоновогоЗадания) Тогда
		ПодключитьОбработчикОжидания("СостояниеРегламентногоЗадания",1);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПросмотрЖурнала(Команда)
	ПараметрыФормы = Новый Структура("Отбор", Новый Структура("Замер", Замер));
	ОткрытьФорму("Обработка.ПросмотрТехнологическогоЖурнала.Форма.Форма", ПараметрыФормы, ЭтаФорма);
КонецПроцедуры


&НаСервере
Процедура ЗапуститьЗагрузкуНаСервере()
	Если ЗначениеЗаполнено(КлючФоновогоЗадания) Тогда
		ФоновоеЗадание =  ФоновыеЗадания.НайтиПоУникальномуИдентификатору(новый УникальныйИдентификатор(КлючФоновогоЗадания));
		Если НЕ ФоновоеЗадание=Неопределено 
			И ФоновоеЗадание.Состояние=СостояниеФоновогоЗадания.Активно Тогда
			Сообщить("Фоновое задание еще работает");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	мПараметры = новый Массив;
	мПараметры.Добавить(Замер);
	мПараметры.Добавить(КаталогТЖ);
	мПараметры.Добавить(Истина);
	
	ФоновоеЗадание = ФоновыеЗадания.Выполнить("ОбновлениеДанных.ЗагрузкаЖурналаПредварительноОчистить",мПараметры);
	КлючФоновогоЗадания = ФоновоеЗадание.УникальныйИдентификатор;
КонецПроцедуры

&НаКлиенте
Процедура СостояниеРегламентногоЗадания() Экспорт
	
	Если НЕ СостояниеРегламентногоЗаданияСервер(КлючФоновогоЗадания) Тогда
		ОтключитьОбработчикОжидания("СостояниеРегламентногоЗадания");
		КлючФоновогоЗадания = "";
		Сообщение = "Завершено.";
	Иначе
		Прогресс = ПолучитьПрогресс(Замер);
		Сообщение = " "+Прогресс+"%...";
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПрогресс(Замер)
	Прогресс = 0;
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СтатусЗагрузки.Замер КАК Замер,
	|	СтатусЗагрузки.ЧтениеВПроцессе КАК ЧтениеВПроцессе,
	|	СтатусЗагрузки.Прогресс КАК Прогресс,
	|	СтатусЗагрузки.Разобрано КАК Разобрано,
	|	СтатусЗагрузки.Длительность КАК Длительность
	|ИЗ
	|	РегистрСведений.СтатусЗагрузки КАК СтатусЗагрузки
	|ГДЕ
	|	СтатусЗагрузки.Замер = &Замер";
	Запрос.УстановитьПараметр("Замер",Замер);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Прогресс = Выборка.Прогресс;
	КонецЕсли;
	Возврат Прогресс;
КонецФункции

&НаСервереБезКонтекста
Функция СостояниеРегламентногоЗаданияСервер(КлючФоновогоЗадания)
	
	Если ЗначениеЗаполнено(КлючФоновогоЗадания) Тогда
		ФоновоеЗадание =  ФоновыеЗадания.НайтиПоУникальномуИдентификатору(новый УникальныйИдентификатор(КлючФоновогоЗадания));
		Если ФоновоеЗадание=Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
		Если НЕ ФоновоеЗадание.Состояние=СостояниеФоновогоЗадания.Активно Тогда
			Возврат Ложь;
		Иначе
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

&НаСервере
Функция ПроверитьСуществованиеКаталога(ИмяКаталога) Экспорт

	КаталогНаДиске = Новый Файл(ИмяКаталога);
	Если КаталогНаДиске.Существует() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции 

&НаКлиенте
Процедура ЗагрузитьЖурнал(Команда)
	Если ПроверкаВозможностиЗагрузки() = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьЖурналНаСервере();	
КонецПроцедуры

&НаКлиенте
Функция ПроверкаВозможностиЗагрузки()
	Если НЕ ЗначениеЗаполнено(КаталогТЖ) Тогда
		Сообщить("Укажите каталог ТЖ прежде!");
		Возврат Ложь;
	КонецЕсли;
	Если НЕ ПроверитьСуществованиеКаталога(КаталогТЖ) Тогда
		Сообщить("Нет доступа к каталогу "+КаталогТЖ);	
		Возврат Ложь;
	КонецЕсли;
	Если НовыйЗамер=1 Тогда
		Если НЕ ЗначениеЗаполнено(Замер) Тогда
			Сообщить("Укажите замер или поставьте СтраницаНовый замер!");
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Если СоздатьЗамерНаСервере() = Ложь Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции 	

&НаСервере
Функция СоздатьЗамерНаСервере()
	ЗамерОбъект = Справочники.Замеры.СоздатьЭлемент();
	ЗамерОбъект.Наименование = ?(ЗначениеЗаполнено(НаименованиеЗамера),НаименованиеЗамера, "Из каталога: "+КаталогТЖ);
	ЗамерОбъект.ДатаЗамера = ТекущаяДата();
	ЗамерОбъект.СерверныйТЖ = СерверныйТЖ;
	ЗамерОбъект.НеРабочийКаталог = НеРабочийКаталог;
	Попытка
		ЗамерОбъект.Записать();
		Замер = ЗамерОбъект.Ссылка;
		//изменим форму сразу
		НовыйЗамер = 1;
		УстановитьСтраницы();
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;
	Возврат Истина;
КонецФункции

&НаКлиенте
Процедура КаталогТЖНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога); 
	Диалог.Заголовок = "Выберите каталог"; 
	Если ЗначениеЗаполнено(ЭтаФорма.КаталогТЖ) Тогда
		Диалог.Каталог = ЭтаФорма.КаталогТЖ;
	КонецЕсли;
	Диалог.МножественныйВыбор = Ложь; 
	ВыборФайлаОткрытияФайла = новый ОписаниеОповещения("ВыборФайлаОткрытияКаталога",ЭтотОбъект,Новый Структура("ИмяЭлемента","КаталогТЖ"));
	Диалог.Показать(ВыборФайлаОткрытияФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаОткрытияКаталога(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено И ВыбранныеФайлы.Количество() > 0 Тогда
		ИмяЭлемента = "";
		Если ДополнительныеПараметры.Свойство("ИмяЭлемента",ИмяЭлемента) Тогда
			ЭтаФорма[ИмяЭлемента] = ВыбранныеФайлы[0]; 
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

