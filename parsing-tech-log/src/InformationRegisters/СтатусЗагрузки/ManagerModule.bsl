Функция ЗаписатьНачалоЧтения(ЗНАЧ Замер) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СтатусЗагрузки.Замер
	|ИЗ
	|	РегистрСведений.СтатусЗагрузки КАК СтатусЗагрузки
	|ГДЕ
	|	СтатусЗагрузки.Замер = &Замер
	|	И СтатусЗагрузки.ЧтениеВПроцессе = ИСТИНА";
	Запрос.УстановитьПараметр("Замер", Замер);
	Если Запрос.Выполнить().Пустой() Тогда
		МенеджерЗаписи = РегистрыСведений.СтатусЗагрузки.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Замер = Замер;
		МенеджерЗаписи.ДатаНачала = ТекущаяДата();
		МенеджерЗаписи.ЧтениеВПроцессе = Истина;
		МенеджерЗаписи.Прогресс = 0;
		МенеджерЗаписи.Разобрано = 0;
		МенеджерЗаписи.Длительность = 0;
		МенеджерЗаписи.Записать(Истина);
		
		Результат = Истина;
	Иначе
		Результат = Ложь;
		ЗаписьЖурналаРегистрации("ЧтениеЖурналаРегистрации.ЧтениеОтклонено");
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

//записывает прогресс для замера находящего в состоянии загрузки
Процедура ЗаписатьПрогресс(Замер, Прогресс, Разобрано) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СтатусЗагрузки.Замер,
	|	СтатусЗагрузки.ЧтениеВПроцессе,
	|	&Прогресс КАК Прогресс,
	|	&Разобрано КАК Разобрано,
	|	СтатусЗагрузки.Длительность,
	|	СтатусЗагрузки.ДатаНачала
	|ИЗ
	|	РегистрСведений.СтатусЗагрузки КАК СтатусЗагрузки
	|ГДЕ
	|	СтатусЗагрузки.Замер = &Замер
	|	И СтатусЗагрузки.ЧтениеВПроцессе = ИСТИНА";
	Запрос.УстановитьПараметр("Замер", Замер);
	Запрос.УстановитьПараметр("Прогресс", Прогресс);
	Запрос.УстановитьПараметр("Разобрано", Разобрано);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		МенеджерЗаписи = РегистрыСведений.СтатусЗагрузки.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Записать();		
	КонецЕсли;
КонецПроцедуры

Процедура ЗаписатьЗавршениеЧтения(Замер, Прогресс, Разобрано, Длительность = 0) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СтатусЗагрузки.Замер,
	|	ЛОЖЬ КАК ЧтениеВПроцессе,
	|	&Прогресс КАК Прогресс,
	|	&Разобрано КАК Разобрано,
	|	&Длительность КАК Длительность,
	|	СтатусЗагрузки.ДатаНачала
	|ИЗ
	|	РегистрСведений.СтатусЗагрузки КАК СтатусЗагрузки
	|ГДЕ
	|	СтатусЗагрузки.Замер = &Замер
	|	И СтатусЗагрузки.ЧтениеВПроцессе = ИСТИНА";
	Запрос.УстановитьПараметр("Замер", Замер);
	Запрос.УстановитьПараметр("Прогресс", Прогресс);
	Запрос.УстановитьПараметр("Разобрано", Разобрано);
	Запрос.УстановитьПараметр("Длительность", Длительность);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		МенеджерЗаписи = РегистрыСведений.СтатусЗагрузки.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Записать();		
	КонецЕсли;	
КонецПроцедуры
